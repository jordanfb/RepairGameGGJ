title: SHIP
tags: beginning the game
colorID: 1
position: 59,145
---
<<if not visited("SHIP")>>
	AI: Uhmm... excuse me
	<<wait 1>> 
	AI: CAPTAIN, you need to wake up now...
	<<wait 3>> 
	<<fadein>>
	AI: HEYYY!!!!
	<<wait 1>> 
	AI: A-hem, sorry about that but Galatic protocol requires I inform you that the vessel has sustained some significant damage during our crash landing.
	AI: Three core components, the COSMIC CUBES, have dislodged and need to be reinstalled for repairs to be complete.
	AI: And that's where you come in CAPTAIN. You are going to need to locate the missing COSMIC CUBES which have been scattered across the landscape.
	AI: Goodluck CAPTAIN
	<<wait .5>>
	AI: Oh almost forgot. Take this RADIO as well. That way I can keep in touch and inform you of any developments. Alright, off you go.
	<<stop>>
<<elseif collected("mesacube") and collected("cavecube") and collected("hotspringscube") and not visited("SHIPASSESSMENT")>>
	AI: Good work collecting all the COSMIC CUBES, CAPTAIN.
	[[What's next?|SHIPASSESSMENT]]
<<elseif ((collected("mesacube") and collected("cavecube")) or (collected("hotspringscube") and collected("cavecube")) or (collected("hotspringscube") and collected("mesacube"))) and not visited("SHIPASSESSMENT")>>
	AI: Still one more COSMIC CUBE to collect!
<<elseif (collected("mesacube") or collected("cavecube") or collected("hotspringscube")) and not visited("SHIPASSESSMENT")>>
	AI: Two more components to collect, CAPTAIN!
<<elseif not visited("SHIPASSESSMENT")>>
	AI: Go find the three COSMIC CUBES!
<<endif>>
===
title: HOTSPRINGS
tags: SUZAN
colorID: 6
position: 295,143
---
AI: CAPTAIN, my sensors are indicating that a COSMIC CUBE is nearby. I sense some natural water source...hot springs perhaps? 

AI: Incoming transmission. Message source SUZAN
<<wait 1>> 
SUZAN: Hey, hello?!
SUZAN: Come on pick up!
SUZAN: Ugh...with all the advancements in interstellar technology, still getting crappy service
SUZAN: If you get this, please call me back
SUZAN: No, please come back
SUZAN: I'm sure we can work this out together. I know you're better than what everyone is saying
SUZAN: I know you did not mean to...it must have been an accident. I even have some proof...I think
SUZAN: No matter what happens, I love you
<<wait 1>> 
AI: End transmission
<<wait 1>> 
AI: SUZAN, I can see a drawing from an artist of the same name. records indicates SUZAN is...your daughter
<<wait 1>> 
AI: Message was received shortly before crash landing occurred. Unfortunately until repairs are finished interstellar communication services will not be operational.
AI: Last significant transmission before this with SUZAN was recorded 3 years ago. 
<<wait 1>> 
AI: I would recommend calling her back... 
===
title: HOTSPRINGS.CUBE
tags: BANANA SALAMANDAER MEDIA
colorID: 0
position: 805,105
---
<<if collected("mesacube") and collected("cavecube")>> 
   AI: Alright, you have successfully acquired all three COSMIC CUBES. Now repairs can be completed, please return to the ship.
<<else>> 
   AI: Good work but there's no time to relax CAPTAIN.
<<endif>> 
===
title: CAVE
tags: MATTE
colorID: 0
position: 533,135
---
AI: CAPTAIN, sensors indicate a COSMIC CUBE is within the heart of the cave up ahead. 
<<wait 1>> 
AI: Proceed with caution, as limited light sources are available.

Incoming secure transmission. Message source SHERIFF MATTE
<<wait 1>> 
MATTE: Dang it, I can't believe you escaped my clutches yet again. Argh...what is wrong with me.
MATTE: I can't believe it...never in a thousand years would I ever have pegged you as a...
MATTE: You're just like the scum we swore we'd put behind bars. What happened to our promise to keep the streets safe huh?
MATTE: I'm so annoyed, and disgusted and... sad, I just don't want to believe it
MATTE: The past six months chasing you over the galaxy have been trying both physically and mentally. You were my partner so we know each other in and out...but I'm going to say this
<<wait 1>> 
MATTE: Don't come back. part of me is happy you have eluded capture for so long. And you know how the department is always understaffed.
MATTE: So don't return, if you do...you know I'll have to carry out my duty
MATTE: Be safe out there...partner
<<wait 1>> 
AI: End transmission
<<wait 1>> 
AI: Ship records indicate that SHERIFF MATTE is a member of the Galatic Patrol Head of sector XQVIX. Recent records show that he was recently promoted to that position roughly six months ago after the dishonorable discharge of...
<<wait 1>> 
AI: Uhm...O-ohh never mind CAPTAIN. I didn't mean to... I mean it's not as if... 
<< wait 1>> 
AI: ...Forget I said anything
===
title: CAVE.CUBE
tags: BANANA SALAMANDAER MEDIA
colorID: 0
position: 810,105
---
<<if collected("mesacube") and collected("hotspringscube")>> 
   AI: Alright, you have successfully acquired all three COSMIC CUBES. Now repairs can be completed, please return to the ship.
<<else>> 
   AI: Good find. Time to head back out CAPTAIN.
<<endif>> 
===
title: MESA
tags: BANANA SALAMANDAER MEDIA
colorID: 0
position: 786,105
---
AI: CAPTAIN, my sensors are indicating that a COSMIC CUBE is nearby. Sensors indicate it maybe near the zenith of the MESA. 
AI: Sensors also indicate that weather conditions have dropped dramatically, auto engaging climate control suit. 
AI: You're set to go CAPTAIN.

Incoming transmission Banana Salamander Media
<<wait 1>> 
BSM: So we're here with the latest scoop on the story that has rocked the Galaxy.
BSM: Yes, the case of the renegade Sheriff whom murdered his partner in cold blood leaving behind their adopted daughter
BSM: What more, the fugitIve has been on the run for the past 6 months from the Galactic Patrol forces.
BSM: A super dangerous individual with no regard for human life!
BSM: If you see them, do not approach them before you become a victim too
BSM: For all the latest gossip from one corner of the galaxy to the next, tune into Banana Salamander Media.

AI: End transmission. 
<<wait 1>> 
AI: Records indicate that Banana Salamander Media is a hodge podge media outfit peddling sensational news and content with questionable accuracy. They are notorious for rampant misinformation and FAKE NEWS across the galaxy.
<<wait 1>> 
AI: CAPTAIN, I would recommend disregarding the contents of that last transmission. It somehow bypassed SPAM filters due to the current state of disrepair.
===
title: MESA.CUBE
tags: BANANA SALAMANDAER MEDIA
colorID: 0
position: 799,105
---
<<if collected("hotspringscube") and collected("cavecube") >> 
   AI: Alright, you have successfully acquired all three COSMIC CUBES. Now repairs can be completed, please return to the ship.
<<else>> 
   AI: Nice work CAPTAIN.
<<endif>> 
===
title: SHIPASSESSMENT
tags: FINAL PREP
colorID: 0
position: 1008,107
---
AI: Verifying COSMIC CUBES
AI: Reinstating warp drives and transport features
AI: Reinstating interstellar communication elements and navigation
AI: REBOOTING 
AI: .
AI: ..
AI: ...
AI: Running final diagnostics, Green light confirmed
AI: ALL SYSTEMS OPERATIONAL
<< wait 1>> 
AI: What are you going to do?
[[CHOICES]]
===
title: CHOICES
tags: FINAL CHOICES
colorID: 0
position: 1018,107
---
<<if visited("CHOICES")>>
	AI: What are your orders?
<<endif>>
[[I want to go back home|CHOICES.HOME.CHECK]]
[[I've got to keep flying|CHOICES.AWAY.CHECK]]
[[I'm going to stay here|CHOICES.STAY.CHECK]]
===
title: CHOICES.HOME.CHECK
tags: FINAL CHOICES
colorID: 0
position: 1018,107
---
AI: You desire to return to your home planet?
AI: You do know that SHERIFF MATTE will be there?
[[Yeah I'm sure. Let's do this.|CHOICES.HOME.GO]]
[[Hmmm. I'm not certain.|CHOICES]]
===
title: CHOICES.HOME.GO
tags: FINAL CHOICES
colorID: 0
position: 1018,107
---
AI: Returning to launch destination. Recalibrating coordinates and plotting new trajectory
AI: CAPTAIN, if I may be so bold
<<wait 2>> 
AI: Logic dictates that this course of action may have some severe repercussions. However, it also provides an avenue by which you can repair what has been damaged.
AI: I possess all relevant records intact from the prior year which can be processed accordingly by the relevant authorities.
AI: Things will work out somehow CAPTAIN.
<<wait 1>> 
AI: Incoming transmission... caller ID SUZAN
<<wait 1>> 
AI: Call Accepted
SUZAN: Hello...?
-> I'm coming home.
SUZAN: Oh, thank goodness!
<<exittomainmenu>>
===
title: CHOICES.AWAY.CHECK
tags: FINAL CHOICES
colorID: 0
position: 1018,107
---
AI: Final destination set for the TOFFEE NOVEL SYSTEM
AI: Location is not currently governed by the Galatic Patrol elements. Travel to this System is prohibited by my safety regulations.
AI: In order for you to complete travel to this location I will have to override my security protocols and go off-line.
AI: Confirm your order?
[[Confirmed.|CHOICES.AWAY.GO]]
[[Hmmm. I'm not certain.|CHOICES]]
===
title: CHOICES.AWAY.GO
tags: FINAL CHOICES
colorID: 0
position: 1018,107
---
AI: So the decision is to run away? I cannot say I understand your command CAPTAIN but my programming is to support you in your endeavors. I will now override my security protocols and go off-line.
AI: So from this point onwards CAPTAIN, I am afraid you will be on your own.
Ai: Trajectory set and Auto-pilot engaged. Shutting down System security protocols and interface. 
<<wait 1>> 
AI: Have a safe journey and please be safe, CAPTAIN.
<<exittomainmenu>>
===
title: CHOICES.STAY.CHECK
tags: FINAL CHOICES
colorID: 0
position: 1018,107
---
AI: Hmmm, Uhm... CAPTAIN, are you certain this is the best course of action?
<<wait 1>> 
AI: Preliminary scans of the planet indicate it is currently not inhabited but possess limited resources.
AI: Current supplies are estimated to last for a month at best before survival cannot be guaranteed.
<<wait 1>> 
AI: If that is your choice CAPTAIN, then systems will immediately go into life support mode to last as long as possible.
AI: However, that will shut down the main interface system, so I will not be able to offer assistance.
[[Abort the launch, I'm staying here.|CHOICES.STAY.GO]]
[[Hmmm. I'm not certain.|CHOICES]]
===
title: CHOICES.STAY.GO
tags: FINAL CHOICES
colorID: 0
position: 1018,107
---
AI: I am not sure I understand this decision but my job is to facilitate your orders so shut down sequence will begin momentarily.
AI: Wait CAPTAIN!!!
AI: Sensors are picking up several hostile creatures approaching the proximity of the vessel. Initial scans appear to indicate they are nocturnal carnivorous species.
<<wait 1>> 
AI: CAPTAIN please be safe out th...
<<exittomainmenu>>
===